description = "H2O Integration Testing"

dependencies {
    compile project(":h2o-assembly")
}

def runner = new File("$rootDir/h2o-r/tests/run.py").canonicalPath


// Python MultiJVMTests
// --------------------

def pythonMultiJVMTestsPath = new File("$rootDir/py/testdir_multi_jvm")

task runPythonMultiJVMTests(type: Exec) {
    workingDir pythonMultiJVMTestsPath
    commandLine 'python', runner, '--wipeall', '--numclouds', '1'
}

task cleanPythonMultiJVMTests(type: Delete) {
    delete "$pythonMultiJVMTestsPath/results"
}

test.dependsOn smalldataCheck
test.dependsOn runPythonMultiJVMTests
clean.dependsOn cleanPythonMultiJVMTests


// NodeJS SingleJVMTests
// ---------------------

def nodeJsSingleJVMTestsPath = new File("$rootDir/h2o-web")

task runNodeJsSingleJVMTests(type: Exec) {
    workingDir nodeJsSingleJVMTestsPath
    commandLine 'python', runner, '--wipeall', '--numclouds', '1', '--test', 'src/main/resources/www/steam/js/steam-tests.js', '--jvm.xmx', '4g'
}

task cleanNodeJsSingleJVMTests(type: Delete) {
    delete "$nodeJsSingleJVMTestsPath/results"
}

// Commented out on 12/15/2014
// test.dependsOn runNodeJsSingleJVMTests
clean.dependsOn cleanNodeJsSingleJVMTests


// REST API Documentation generator
// --------------------------------

def restGenPath = new File("$rootDir/py")

task runGenerateRESTAPIDocs(type: Exec) {
    workingDir restGenPath
    commandLine 'python', runner, '--wipeall', '--numclouds', '1', '--jvm.xmx', '4g', '--test', 'generate_rest_api_docs.py'
}

task cleanGenerateRESTAPIDocs(type: Delete) {
    delete "$restGenPath/results"
}

test.dependsOn runGenerateRESTAPIDocs
clean.dependsOn cleanGenerateRESTAPIDocs
