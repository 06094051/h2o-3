apply plugin: 'fatjar'
apply plugin: 'application'

description = "H2O Application Assembly"

dependencies {
  compile project(":h2o-web")
  compile project(":h2o-algos")
  compile project(":h2o-core")
  compile project(":h2o-app")
  // Add h2o-flow which is external project
  compile("ai.h2o:h2o-flow:$h2oFlowVersion")
}

fatJar {
   manifest {
      attributes 'Main-Class': 'water.H2OApp'
   }
}

// Setup Application plugin
mainClassName = 'water.H2OApp'

//
// Support make infrastructure by copying the resulting assembly into parent
// project build directory
//

def assembly = "h2o-assembly.jar"
def allInOne = "h2o.jar"

task copyJar(type: Copy) {
    from ("${project.buildDir}/libs"){
        include assembly
    }
    into "${project.parent.buildDir}"
    rename { it.replace(assembly, allInOne) }
}
// Execute always copyJar
fatJar.finalizedBy copyJar
// Run fatJar as par of build
build.dependsOn fatJar

